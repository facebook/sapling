# @noautodeps

load("@fbcode_macros//build_defs:python_library.bzl", "python_library")
load("@fbcode_macros//build_defs:python_unittest.bzl", "python_unittest")
load("//eden:defs.bzl", "get_test_env_and_deps")

oncall("mercurial_release")

ENV = {
    "HGTEST_HG": "$(location //eden/scm:hg)",
}

python_library(
    name = "repotestlib",
    srcs = glob(
        ["*.py"],
        exclude = ["tests.py"],
    ),
    deps = [
        "fbsource//third-party/pypi/requests:requests",
        "//eden/integration/lib:lib",
        "//eden/scm/lib/hgcommands:hgcommands",
        "//eden/scm/saplingnative/pybindings:bindings",
        "//eden/scm/tests:watchman",
        "//eden/test_support:py",
        "//libfb/py:thrift",
    ],
)

python_unittest(
    name = "tests",
    srcs = ["tests.py"],
    env = ENV,
    resources = {
        "//eden/scm:hg": "hg.sh",
    },
    deps = [
        ":repotestlib",
    ],
)

# Run all tests with the default build type (the empty suffix)
artifacts = get_test_env_and_deps("")

EDEN_ENV = artifacts["env"]

EDEN_ENV.update(ENV)

EDEN_ENV["USE_EDEN"] = "1"

python_unittest(
    name = "tests_eden",
    srcs = ["tests.py"],
    compatible_with = [
        "ovr_config//os:linux",
    ],
    env = EDEN_ENV,
    resources = {
        "//eden/fs/cli:edenfsctl": "edenfsctl",
        "//eden/fs/cli_rs/edenfsctl:edenfsctl": "edenfsctl_rs",
        "//eden/fs/service:edenfs": "edenfs",
        "//eden/fs/service:edenfs_privhelper": "edenfs_privhelper",
        "//eden/scm:hg": "hg.sh",
    },
    runtime_deps = artifacts["deps"],
    deps = [
        ":repotestlib",
    ],
)

MONONOKE_ENV = dict(ENV)

MONONOKE_ENV["USE_MONONOKE"] = "1"

MONONOKE_ENV["HGTEST_MONONOKE_SERVER"] = "$(location //eden/mononoke:mononoke)"

MONONOKE_ENV["HGTEST_CERTDIR"] = "$(location //eden/mononoke/tests/integration/certs/facebook:test_certs)"

python_unittest(
    name = "tests_mononoke",
    srcs = ["tests.py"],
    compatible_with = [
        "ovr_config//os:linux",
    ],
    env = MONONOKE_ENV,
    resources = {
        "//eden/mononoke:mononoke": "mononoke",
        "//eden/scm:hg": "hg.sh",
    },
    runtime_deps = [
        "//eden/mononoke/tests/integration/certs/facebook:test_certs",
    ],
    deps = [
        ":repotestlib",
        "//configerator/structs/scm/mononoke/repos:repos-python-types",
    ],
)
